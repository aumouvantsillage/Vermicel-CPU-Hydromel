#lang hydromel

# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

import "decoder.mel"

component arith_logic_unit
    port instr : splice flip instruction
    port a     : in word_t
    port b     : in word_t
    port r     : out word_t

    signal sh = b{5 .. 0}

    r = case alu_fn of
        alu_nop  => b
        alu_add  => (a + b) as word_t
        alu_sub  => (a - b) as word_t
        alu_slt  => (a as integer) < (b as integer)
        alu_sltu => a < b
        alu_xor  => a xor b
        alu_or   => a or  b
        alu_and  => a and b
        alu_sll  => (a << sh) as word_t
        alu_srl  => a >> sh
        alu_sra  => ((a as integer) >> sh) as word_t
end
