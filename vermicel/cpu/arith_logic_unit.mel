#lang hydromel

# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

import "common.mel"
import "decoder.mel"

component arith_logic_unit
    port instr : in instruction_t
    port a     : in word_t
    port b     : in word_t
    port r     : out word_t

    signal sh = b{5 .. 0}

    r = case instr.alu_fn of
        ~alu_nop  => b
        ~alu_add  => word_t|a + b|
        ~alu_sub  => word_t|a - b|
        ~alu_slt  => integer|a| < integer|b|
        ~alu_sltu => a < b
        ~alu_xor  => a xor b
        ~alu_or   => a or  b
        ~alu_and  => a and b
        ~alu_sll  => word_t|a << sh|
        ~alu_srl  => a >> sh
        ~alu_sra  => word_t|integer|a| >> sh|
end
